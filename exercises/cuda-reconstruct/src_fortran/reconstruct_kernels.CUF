!
!     This is a CUDA FORTRAN module that contains different variants of
!     reverse edge-detection algorithm.
!
!     Training material developed by George Beckett, based on original
!     material provided by James Perry, Alan Gray, and David Henty.
!
!     Copyright EPCC, The University of Edinburgh, 2011

#define SOLUTION 

module reverse_kernels
  implicit none

  integer, parameter :: tp = kind(1.0)
  integer, parameter :: rp = kind(1.0d0)

contains

  attributes(global) subroutine inverseEdgeDetect(height, width, &
       source, destination, edge)
    implicit none
    
    integer, value, intent(in) :: height, width ! Passing by value to avoid need to
                                                ! assign in advance of call

    real (kind=rp), dimension((height+2),(width+2)), intent(in)    :: source, edge
    real (kind=rp), dimension((height+2),(width+2)), intent(inout) :: destination

    integer :: row, col

    ! Calculate global row index for this thread
#ifdef SOLUTION 
    row = ((blockidx%x-1)*blockdim%x + threadidx%x) + 1 
#else 
    col = ((blockidx%x-1)*blockdim%x + threadidx%x) + 1
#endif 

#ifdef SOLUTION 
    do col=2,height+1 
#else 
    do row=2,height+1
#endif 
       destination(row,col) = 0.25* &
            (source(row-1,col) + &
            source(row+1,col) + &
            source(row,col-1) + &
            source(row,col+1) - &
            edge(row,col))
    end do

  end subroutine inverseEdgeDetect




  attributes(global) subroutine inverseEdgeDetect2D(height, width, & 
       source, destination, edge) 
    implicit none 
    
    integer, value, intent(in) :: height, width ! Passing by value to avoid need to 
                                                ! assign in advance of call 

    real (kind=rp), dimension((height+2),(width+2)), intent(in)    :: source, edge 
    real (kind=rp), dimension((height+2),(width+2)), intent(inout) :: destination 

    integer :: row, col 

    ! Calculate global row and column indices for this thread 
    row = ((blockidx%x-1)*blockdim%x + threadidx%x) + 1 
    col = ((blockidx%y-1)*blockdim%y + threadidx%y) + 1 

    destination(row,col) = 0.25* & 
         (source(row-1,col) + & 
         source(row+1,col) + & 
         source(row,col-1) + & 
         source(row,col+1) - & 
         edge(row,col)) 

  end subroutine inverseEdgeDetect2D 
end module reverse_kernels
